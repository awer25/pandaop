import os
import platform

CC = 'gcc'
system = platform.system()
if system == 'Darwin':
  # gcc installed by homebrew has version suffix (e.g. gcc-12) in order to be
  # distinguishable from system one - which acts as a symlink to clang
  CC += '-13'

env = Environment(
  CC=CC,
  CFLAGS=[
    '-nostdlib',
    '-fno-builtin',
    '-std=gnu11',
    '-Wfatal-errors',
    '-Wno-pointer-to-int-cast',
  ],
  CPPPATH=[".", "../../board/"],
)

if system == "Darwin":
  env.PrependENVPath('PATH', '/opt/homebrew/bin')

if GetOption('ubsan'):
  flags = [
    "-fsanitize=undefined",
    "-fno-sanitize-recover=undefined",
  ]
  env['CFLAGS'] += flags
  env['LINKFLAGS'] += flags

if GetOption('safety_coverage'):
  env.Append(
    CFLAGS=["-fprofile-arcs", "-ftest-coverage", "-fprofile-abs-path",],
    LIBS=["gcov"],
  )

original_decider = env.decide_target

def gcno_changed(dependency, target, prev_ni, _):
  return not target.get_dir().File('panda.gcno').exists() or original_decider(dependency, target, prev_ni)

libpanda = env.SharedLibrary("libpanda.so", ["panda.c",])
env.SideEffect("panda.gcno", libpanda)
env.Decider(gcno_changed)
